apply plugin: 'groovy'
apply plugin: 'fatjar'
apply plugin: 'application'
apply plugin: 'idea'

// The main class of the application
mainClassName = 'com.example.helloworld.MainApplication'

// Use Java 7 by default
sourceCompatibility = 1.7
targetCompatibility = 1.7

// Add Gradle FatJar Plugin, see https://github.com/musketyr/gradle-fatjar-plugin
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'eu.appsatori', name: 'gradle-fatjar-plugin', version: '0.1.3'
    }
}

// Set our project variables
project.ext {
    //dropWizardConfig = './src/dist/config/example.yml'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.1'
    // this dependency is needed due to http://issues.gradle.org/browse/GRADLE-2647
    compile "com.sun.jersey:jersey-client:1.15" 
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.6'
}

// Configure the fatJar task
fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

// Configure the run task to start the Dropwizard service

run {
    args 'server' //, dropWizardConfig
}


task stage(dependsOn: ['clean', 'fatJar'])
